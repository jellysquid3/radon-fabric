plugins {
	id 'fabric-loom' version '0.6-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	mavenCentral()
}

configurations {
	bindings
	implementation.extendsFrom bindings
	include.extendsFrom bindings

	natives
	implementation.extendsFrom natives
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

addLwjglBindings('lwjgl-zstd')
addLwjglBindings('lwjgl-lmdb')

def addLwjglBindings(name) {
	def group = 'org.lwjgl'
	def version = '3.2.2'

	dependencies {
		bindings(group: group, name: name, version: version) { transitive = false }

		natives(group: group, name: name, version: version, classifier: 'natives-linux') { transitive = false }
		natives(group: group, name: name, version: version, classifier: 'natives-macos') { transitive = false }
		natives(group: group, name: name, version: version, classifier: 'natives-windows') { transitive = false }
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	// The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
	// JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
	// We'll use that if it's available, but otherwise we'll use the older option.
	def targetVersion = 8
	
	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release = targetVersion
	}
}

jar {
	from (project.configurations.natives) {
		into ("natives/radon")
	}

	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
}
